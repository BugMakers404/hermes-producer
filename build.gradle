plugins {
    id 'org.springframework.boot' version '3.0.2'
    id 'com.palantir.git-version' version '2.0.0'
    id "com.avast.gradle.docker-compose" version "0.16.11"
}

allprojects {
    apply plugin: 'base'
}


subprojects {
    apply plugin: 'com.palantir.git-version'
    apply plugin: 'java-library'
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    group 'org.bugmakers404.hermes'
    def versionDetails = versionDetails()
    def versionSuffix  = project.hasProperty('release') ? '-RELEASE' : '-SNAPSHOT'
    version = versionDetails.lastTag  + versionSuffix

    repositories {
        mavenCentral()
    }

    test {
        useTestNG()
    }
}

def userName = findProperty('hermesMongoRootUserName').toString()
def password = findProperty("hermesMongoRootPassword").toString()

dockerCompose {
    environment.put "HERMES_MONGODB_ROOT_USER_NAME", userName
    environment.put "HERMES_MONGODB_ROOT_USER_PASSWORD", password
    useComposeFiles = ['docker/local/docker-compose.yaml']
}







dependencies {
    implementation project(':hermes-producer-vicroad')
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        exclude group: 'junit', module: 'junit'
    }

    testImplementation 'org.testng:testng:7.7.1'
    testImplementation 'org.apache.httpcomponents:httpclient:4.5.14'
}

springBoot {
    mainClass = 'org.bugmakers404.hermes.producer.Application'
}

bootRun {
    // support passing -Dsystem.property=value to bootRun task
    // systemProperties is the jvm properties to run the springboot application
    // Gradle uses the standard Java system properties to provide various configuration options and runtime information
    systemProperties = System.properties as Map<String, ?>

    // Apply the external application.properties
    // Please note do not use hermesProducerAppProps directly since CI server does not store it
    if (rootProject.ext['appProps'] != null) {
        systemProperty "spring.config.additional-location", rootProject.ext['appProps']
    }
}

jar {
    enabled = false
}

bootJar {
    enabled = true
    from(hermesProducerAppProps) {
        into 'BOOT-INF/classes'
        rename 'hermes-producer-application.properties', 'application.properties'
    }
}